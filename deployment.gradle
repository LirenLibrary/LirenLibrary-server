ant.taskdef(name: 'sshexec', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
        classpath: configurations.jsch.asPath)

ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
        classpath: configurations.jsch.asPath)


ext {
    tomcatHome = System.properties['tomcatHome']
    sshHost = System.properties['sshHost']
    sshPass = System.properties['sshPass']
    sshUser = System.properties['sshUser']
    sshPrivateKey = System.properties['sshPrivateKey']
}

task deploy(dependsOn: ['war']) {

    doLast {
        assert tomcatHome != null
        assert sshHost != null
        assert sshPass != null
        assert sshUser != null

        logger.quiet "Stopping remote server..."
        sshexec "${tomcatHome}/bin/shutdown.sh"

        def timeout = 3
        logger.quiet "Waiting for ${timeout} seconds for remote server to be stopped..."
        sleep timeout * 1000

        logger.quiet "Undeploying on remote server..."
        sshexec "rm -rf ${tomcatHome}/webapps/${war.baseName}*"

        logger.quiet "Deploying ${war.archiveName} to remote server..."
        scp war.archivePath.path, "${sshUser}@${sshHost}:${tomcatHome}/webapps/${war.archiveName}"

        logger.quiet "Migrating database..."
        doDbMigration()

        logger.quiet "Starting remote server..."
        sshexec "${tomcatHome}/bin/startup.sh"

        logger.quiet "Deploy successfully."
    }
}

private void doDbMigration() {
    logger.quiet "Preparing liquibase runtime..."

    configurations.liquibase.files.each {
        scp it.path, "${sshUser}@${sshHost}:/tmp"
    }

    def command = "java -jar /tmp/liquibase-core-2.0.5.jar " +
            "--driver=${dbConnectionDriver} " +
            "--classpath=`dirname ${tomcatHome}/webapps/${war.archiveName}`/${war.archiveName} " +
            "--changeLogFile=changelogs/master.xml " +
            "--url=${dbConnectionUrl} " +
            "--username=${dbConnectionUsername} " +
            "--password=${dbConnectionPassword} " +
            "update"

    logger.quiet "Doing liquibase migration..."
    sshexec command
}


private void scp(source, destination) {
    ant.scp(file: source, todir: destination, password: sshPass, verbose: 'true', trust: 'true')
}

private void sshexec(command) {
    ant.sshexec(verbose: true, username: sshUser, host: sshHost, password: sshPass, command: command, trust: 'true')
}