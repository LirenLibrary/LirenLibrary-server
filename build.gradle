apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'jetty'


version = 1.0

archivesBaseName = 'lirenlibrary'

sourceCompatibility = 1.6
targetCompatibility = 1.6


ext {
    junitVersion = '4.11'
    springVersion = '3.1.3.RELEASE'
    hibernateVersion = '4.1.8.Final'
}

configurations {
    liquibase
    mysql
    compile {extendsFrom mysql}
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    compile(
            "org.springframework:spring-core:${springVersion}",
            "org.springframework:spring-beans:${springVersion}",
            "org.springframework:spring-context-support:${springVersion}",
            "org.springframework:spring-orm:${springVersion}",
            "org.springframework:spring-jdbc:${springVersion}",
            "org.springframework:spring-aop:${springVersion}",
            "org.springframework:spring-web:${springVersion}",
            "org.springframework:spring-webmvc:${springVersion}",
            "org.freemarker:freemarker:2.3.19",
            "org.hibernate:hibernate-core:${hibernateVersion}",
            "org.hibernate:hibernate-entitymanager:${hibernateVersion}",
            "c3p0:c3p0:0.9.1.2"
    )

    mysql(
            "mysql:mysql-connector-java:5.1.22"
    )

    liquibase(
            "org.liquibase:liquibase-core:2.0.5"
    )

    providedRuntime(
            "javax.servlet:servlet-api:2.5"
    )


    testCompile(
            "junit:junit:${junitVersion}",
            "org.springframework:spring-test:${springVersion}"
    )
}


ext {
    //TODO: refactor to Plugin
    databaseBaseDir = 'database'
    liquibaseChangeLogFile = "${databaseBaseDir}/changelogs/changelog.xml"
    liquibaseChangeLogTable = 'liquibase_change_log'
    liquibaseChangeLogLockTable = 'liquibase_change_log_lock'
    liquibaseRollbackCount = 1
    dbConnectionDriver = 'com.mysql.jdbc.Driver'
    dbConnectionUsername = 'liren'
    dbConnectionPassword = 'liren'
    dbConnectionUrl = 'jdbc:mysql://localhost:3306/liren'
}


task updateDatabase {
    description 'Update to latest database.'
    group = 'liquibase'

    doLast {
        ant {
            ant.path(id: 'mysqlClasspath') {
                ant.pathelement(path: configurations.mysql.asPath)
            }

            taskdef resource: 'liquibasetasks.properties', classpath: configurations.liquibase.asPath

            updateDatabase(
                    changeLogFile: liquibaseChangeLogFile,
                    driver: dbConnectionDriver,
                    url: dbConnectionUrl,
                    username: dbConnectionUsername,
                    password: dbConnectionPassword,
                    promptOnNonLocalDatabase: 'false',
                    dropFirst: 'false',
                    classpathref: 'mysqlClasspath',
                    databasechangelogtablename: liquibaseChangeLogTable,
                    databasechangeloglocktablename: liquibaseChangeLogLockTable
            )
        }
    }
}

task rollbackDatabase {
    description 'Rollback liquibase migration.'
    group 'liquibase'

    doLast {
        ant {
            ant.path(id: 'mysqlClasspath') {
                ant.pathelement(path: configurations.mysql.asPath)
            }

            taskdef resource: 'liquibasetasks.properties', classpath: configurations.liquibase.asPath

            rollbackDatabase(
                    changeLogFile: liquibaseChangeLogFile,
                    driver: dbConnectionDriver,
                    url: dbConnectionUrl,
                    username: dbConnectionUsername,
                    password: dbConnectionPassword,
                    classpathref: 'mysqlClasspath',
                    rollbackcount: liquibaseRollbackCount,
                    databasechangelogtablename: liquibaseChangeLogTable,
                    databasechangeloglocktablename: liquibaseChangeLogLockTable
            )
        }
    }
}

jettyRun {
    httpPort 8080
    contextPath 'lirenlibrary'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.3'
}